---
layout: post
title:  Exercise 4.21
date:   2016-11-04
description: Post on 4 November,2016 by Gummary
categories:
- blog
---

这个题讲的是著名的Y Combinator（然而这本书仅在脚注里简单的介绍了一下Orz

下面我尝试用scheme来推导出书上求阶乘的例子

书上的要求是不使用letrec，define来表示递归，而我们再来看一下阶乘

(F n) = (* n (F (- n 1)))

n = 1时返回1。

如果我们用lambda来表示F，则有

~~~scheme
(lambda (n)
  (if (= n 0)
    1
    (* n (<self>(- n 1)))))
~~~

可以看到，由于lambda实现的是匿名函数，我们缺少一个self来表达自身，如果我们用let为这个过程定义一个名字

~~~scheme
(let ((F (lambda (n)
	   (if (= n 0)
	     1
	     (* n (F (- n 1)))))))
  (F n))
~~~

可以看到在let体内的F是未定义的，也就是let无法使用，原因就是let只是lambda的一个语法糖。

这也就解释了在练习4.20中Louis的不严谨之处。

再回头看我们写的第一个表达式，缺少一个self所以无法完成定义，如果我们将self作为参数传入，那么就变为

~~~scheme
(lambda (n self)
  (if (= n 0)
    1
    (* n (self (- n 1)))))
~~~

此时这个式子就变成了一个良好的定义，但是又出现了新的问题，那就是如何调用它。

这里，我们用self指代自身，也就是这个lambda表达式，那么我们在调用的时候把这个表达式再写一遍作为参数传递进去不就可以了？所以我们有

~~~scheme
(lambda (n)
  ((lambda (self n)
     (if (= n 1)
       1
       (* n (self (- n 1)))))
   (lambda (self n)
     (if (= n 1)
       1
       (* n (self (- n 1)))))
   n))
~~~

但是如果运行这个式子的话，可以发现scheme解释器直接报错了，原因是作为参数的lambda表达式需要两个参数，而在作为过程的lambda表达式中调用第二个lambda只用了一个参数(- n 1)，所以不能正确的运行。

解决的方式其实很简单，就是在将self作为参数再加上即可，这里要注意的是，因为内外两个lambda式是同一个，所以都要进行修改，修改后的结果为

~~~scheme
(lambda (n)
  ((lambda (self n)
     (if (= n 1)
       1
       (* n (self self (- n 1)))))
   (lambda (self n)
     (if (= n 1)
       1
       (* n (self self (- n 1)))))
   n))
~~~

我们将这个表达式整理一下，将中间两个相同的表达式用fact表示，就会得到书上给的例子

~~~scheme
((lambda (n)
   ((lambda (fact)
      (fact fact n))
    (lambda (ft k)
      (if (= k 1)
          1
          (* k (ft ft (- k 1)))))))
 10)
~~~

斐波那契数列的表示为

~~~scheme
((lambda (n)
   ((lambda (fact)
      (fact fact n))
    (lambda (ft k)
      (cond ((= k 0) 0)
	    ((= k 1) 1)
	    (else (+ (ft ft (- k 2)) (ft ft (- k 1))))))))
 7)
~~~

### b)


~~~scheme
(define (f x)
  ((lambda (x)
     ((lambda (even? odd?)
        (even? even? odd? x))
      (lambda (ev? od? n)
        (if (= n 0) true (od? ev? od? (- n 1))))
      (lambda (ev? od? n)
        (if (= n 0) false (ev? ev? od? (- n 1))))))
   x))
~~~

### 参考资料

1. [康托尔、哥德尔、图灵——永恒的金色对角线](http://mindhacks.cn/2006/10/15/cantor-godel-turing-an-eternal-golden-diagonal/) 

     


 