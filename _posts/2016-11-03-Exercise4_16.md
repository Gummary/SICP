---
layout: post
title:  Exercise 4.16
date:   2016-11-03
description: Post on 3 November,2016 by Gummary
categories:
- blog
---

### a)

~~~scheme
(define (lookup-variable-value var env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
	     (if (eq? (car vals) '*unassigned*)
	       (error "UNASSIGNED VARS -- LOOK-VARIABLE-VALUE" var)
	       (car vals)))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
~~~

### b)

这个题的答案转自[](https://wizardbook.wordpress.com/2011/01/03/exercise-4-16/) 

首先观察两个转化的式子，是将内部的define转化成let。

我们可以将转化后的式子分成三部分，第一部分是lambda和参数，第二部分是let，第三部分是原式的<e3>

所以我们的转化函数可以为

~~~scheme
(define (scan-out-defines body)
  (let ((defined-vars (definitions body)))
    (if (null? defined-vars)  
        body
        (list 
         (make-let-seq  
          (unassigned-definitions defined-vars)
          (unassigned-initialisations defined-vars)
          (scanned-body body))))))
~~~

**definitions**用于求出所有的定义，形成一个定义的表

~~~scheme
(define (definitions exp)
  (define (scan-iter body definitions-complete)
    (cond ((null? body) null)
          ((definition? (car body))
           (if definitions-complete
               (error "define cannot appear in an expression context - DEFINITIONS" exp)
               (cons (car body) 
                     (scan-iter (cdr body) #f))))
          (else (scan-iter (cdr body) #t))))
  (scan-iter exp #f))
~~~
  
**unassigned-definitions**用于将定义的表达式列表转化为(u '*unsigned*)的形式
          
~~~scheme
(define (unassigned-definitions define-list)
  (map (lambda (def)  
         (list (definition-variable def) 
               '(quote *unassigned*)))
       define-list))
~~~

**unassigned-initialisations**用于求出set！部分

~~~scheme
(define (unassigned-initialisations define-list)
  (map (lambda (def)  
         (list 'set! (definition-variable def) 
               (definition-value def)))
       define-list))
~~~

**scanned-body**用于求出函数体

~~~scheme
(define (scanned-body body)
  (cond ((null? body) body)
        ((definition? (car body)) (scanned-body (cdr body))) 
        (else (cons (car body) 
                    (scanned-body (cdr body))))))
~~~

### c)  

**make-procedure**,在产生过程里使用只做一次即可

~~~scheme
(define (make-procedure parameters body env)
  (list 'procedure parameters (scan-out-defines body) env))
~~~

  
