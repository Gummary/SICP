---
layout: post
title:  Exercise 4.41
date:   2016-11-19
description: Post on 19 November,2016 by Gummary
categories:
- blog
---

思路很简单，首先生成(1,2,3,4,5)的所有组合，然后找出一个符合条件的答案即可。

我们可以使用第二章中的flatmap和fliter过程

~~~scheme
(define (remove x s)
  (filter (lambda (a) (not (= a x)))
	  s))

  
(define (permutation s)
  (if (null? s)
    (list '())
    (flatmap (lambda (x)
	       (map (lambda (p) (cons x p))
		    (permutation (remove x s))))
	     s)))


(define (multiple-dwelling)
  (define (invalid-solution permutation)
    (let ((baker (car permutation))
	  (copper (cadr permutation))
	  (fletcher (caddr permutation))
	  (miller (cadddr permutation))
	  (smith (car (cddddr permutation))))
      (and (not (= baker 5))
	   (not (= copper 1))
	   (not (= fletcher 5))
	   (not (= fletcher 1))
	   (> miller copper)
	   (not (= (abs (- smith fletcher)) 1))
	   (not (= (abs (- fletcher copper)) 1)))))
  (filter invalid-solution (permutation (list 1 2 3 4 5))))
~~~

  